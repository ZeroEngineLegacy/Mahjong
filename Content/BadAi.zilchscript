class BadAi : ZilchComponent
{
  [Dependency] var Player : Player;
  
  [Property] var ProbabilityToKeepTile : Real = 0.7;
  [Property] var ThinkTimingMinMax : Real2 = Real2(1, 5);
  [Property] var SwapTimingMinMax : Real2 = Real2(3, 10);
  
  var Random : RandomContext = null;
  var Hand : PlayerHandManager = null;
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(init, Events.AllObjectsInitialized, this.OnAllObjectsInitialized);
    Zero.Connect(this.Owner, Events.BeginPlayerTurn, this.OnBeginPlayerTurn);
    Zero.Connect(this.Space, Events.RoundBegin, this.OnRoundBegin);
  }

  function OnAllObjectsInitialized(event : CogInitializerEvent)
  {
    // Cache the random class to make life easier
    this.Random = this.LevelSettings.RandomContext;
    this.Hand = this.Player.Hand;
  }

  function OnRoundBegin(event : ZilchEvent)
  {
    // When the round begins start randomly thinking and swapping tiles
    this.QueueTileSwaps();
  }

  function OnBeginPlayerTurn(event : ZilchEvent)
  {
    // Determine how long we want to think
    var thinkTime = this.Random.Range(this.ThinkTimingMinMax.X, this.ThinkTimingMinMax.Y);
    
    // Queue up actions to for our think time
    var seq = Actions.Sequence(this.Owner.Actions);
    Actions.Delay(seq, thinkTime);
    Actions.Call(seq, this.ThinkAboutKeepingTile);
  }
  
  function ThinkAboutKeepingTile()
  {
    // Determine if we should keep our current tile
    var shouldKeepTile = this.Random.Probability(this.ProbabilityToKeepTile);
    if(shouldKeepTile)
    {
      // If so, pick another tile to swap out
      var handSize = this.Player.Hand.CurrentHandSize;
      var indexA = handSize - 1;
      var indexB = this.Random.DieRoll(handSize - 2);
      
      // Swap the two random tiles
      this.Player.SwapTiles(indexA, indexB);
      // Listen for when the swap animation has finished (so we can discard)
      Zero.Connect(this.Hand, Events.FinishedTileSwap, this.OnFinishedSwapToDiscard);
    }
    // Otherwise just discard the new tile
    else
      this.Discard();
  }

  function OnFinishedSwapToDiscard(event : ZilchEvent)
  {
    // Stop listening for a tile swap to finish
    Zero.Disconnect(this.Hand, Events.FinishedTileSwap, this);
    this.Discard();
  }
  
  function Discard()
  {
    this.Player.DiscardTileAndEndTurn(DiscardType.Regular);
  }
  
  function QueueTileSwaps()
  {
    // Wait a certain amount of time before randomly swapping tiles
    var delay = this.LevelSettings.RandomContext.Range(this.SwapTimingMinMax.X, this.SwapTimingMinMax.Y);
    var seq = Actions.Sequence(this.Owner.Actions);
    Actions.Delay(seq, delay);
    Action.Call(seq, this.SwapRandomTiles);
  }
  
  function SwapRandomTiles()
  {
    // Don't swap if it's currently my turn (can screw up the logic to keep a tile)
    if(!this.Player.IsMyTurn)
    {
      // Pick two random tiles to swap (could be the same but who cares)
      var handSize = this.Player.Hand.CurrentHandSize;
      var indexA = this.Random.DieRoll(handSize - 1);
      var indexB = this.Random.DieRoll(handSize - 1);
      this.Player.SwapTiles(indexA, indexB);
    }
    
    // Queue up actions to swap tiles again
    this.QueueTileSwaps();
  }
}
