class PlayerHandManager : ZilchComponent
{
  [Dependency] var Transform : Transform;
  [Dependency] var Orientation : Orientation;
  
  [Static] var HandSize : Integer = 14;
  
  [Property] var TileWidth : Real = 1;
  [Property] var SpaceBetweenTiles : Real = 0.1;
  [Property] var HeightOffset : Real = 0.25;
  [Property] var SelectedHeightOffset : Real = 0.5;
  [Property] var DiscardHandPath : CogPath = CogPath(":/Discards/EastDiscard");
  
  sends FinishedTileSwap : ZilchEvent;
  
  var Tiles : Array[Cog] = Array[Cog]();
  
  var WorldForward : Real3
  {
    get { return this.Orientation.WorldForward; }
  }
  var WorldUp : Real3
  {
    get { return this.Orientation.WorldUp; }
  }
  var TilePosY : Real
  {
    get { return this.Transform.WorldTranslation.Y + this.HeightOffset;}
  }
  var SelectedTilePosY : Real
  {
    get { return this.Transform.WorldTranslation.Y + this.SelectedHeightOffset;}
  }
  var CurrentHandSize : Integer
  {
    get { return this.Tiles.Count; } 
  }
  
  // Converts a index into the hand to the given tile cog
  function TileCogFromIndex(index : Integer) : Cog
  {
    if(0 <= index && index <= this.Tiles.Count)
      return this.Tiles[index];
    return null;
  }
  
  // Converts a tile cog into it's index in the hand. Returns -1 if the cog wasn't in the hand.
  function IndexFromCog(cog : Cog) : Integer
  {
    for(var i = 0; i < this.Tiles.Count; ++i)
    {
      if(this.Tiles[i] == cog)
        return i;
    }
    return -1;
  }
  
  function AddTileNoAnimation(cog : Cog) : Real3
  {
    // Add this tile
    this.Tiles.Add(cog);
    // Listen for when one of our tiles is clicked
    Zero.Connect(cog, Events.LeftMouseDown, this.ForwardEvent);
    
    // Return the position of where the tile should be
    var index = this.Tiles.LastIndex;
    return this.GetTilePosition(index);
  }
  
  function DiscardLastTile(discardType : DiscardType)
  {
    this.DiscardLastTile(discardType, this.Owner.Actions);
  }
  
  function DiscardLastTile(discardType : DiscardType, actionSet : ActionSet)
  {
    var discardIndex = this.Tiles.LastIndex;
    var discardTile = this.Tiles[discardIndex];
    this.Tiles.RemoveAt(discardIndex);
    // Stop listening for the tile being selected since it's no longer in our hand
    Zero.Disconnect(discardTile, Events.LeftMouseDown, this);
    
    // Add the tile to our discard pile
    var discardCog = this.DiscardHandPath.Cog;
    if(discardCog != null)
    {
      discardCog.PlayerDiscardManager.AddToDiscard(discardTile, discardType, actionSet);
    }
  }
  
  function ForwardEvent(event : Event)
  {
    this.Owner.DispatchEvent(event.EventId, event);
  }
  
  function SwapTiles(indexA : Integer, indexB : Integer, speed : Real)
  {
    this.SwapTiles(indexA, indexB, speed, this.Owner.Actions);
  }
  
  function SwapTiles(cogA : Cog, cogB : Cog, speed : Real)
  {
    this.SwapTiles(cogA, cogB, speed, this.Owner.Actions);
  }
  
  function SwapTiles(indexA : Integer, indexB : Integer, speed : Real, actionSet : ActionSet)
  {
    var cogA = this.TileCogFromIndex(indexA);
    var cogB = this.TileCogFromIndex(indexB);
    this.SwapTiles(cogA, cogB, speed, actionSet);
  }
  
  function SwapTiles(cogA : Cog, cogB : Cog, speed : Real, actionSet : ActionSet)
  {
    // Get the indices and positions for these tiles
    var indexA = this.IndexFromCog(cogA);
    var indexB = this.IndexFromCog(cogB);
    var posA = this.GetTilePosition(indexA);
    var posB = this.GetTilePosition(indexB);
    // Swap the actual tile indices
    this.Tiles[indexA] = cogB;
    this.Tiles[indexB] = cogA;
    
    var forwardTime = 0.05;
    var forwardOffset = 0.1;
    var forward = this.WorldForward;
    var forwardA = posA + forward * forwardOffset;
    var forwardB = posB + forward * forwardOffset;
    
    // Compute a normalized time for how fast the tiles should move
    var dist = Math.Distance(posA, posB);
    var normalizedTime = dist / speed;
    
    var seq = Actions.Sequence(actionSet);
    
    // Move both tiles forward slightly (to avoid z-fighting)
    var moveForwardGroup = Actions.Group(seq);
    Actions.Property(moveForwardGroup, @cogA.Transform.WorldTranslation, forwardA, forwardTime, Ease.Linear);
    Actions.Property(moveForwardGroup, @cogB.Transform.WorldTranslation, forwardB, forwardTime, Ease.Linear);
    
    // Swap their positions at a constant speed
    var swapGroup = Actions.Group(seq);
    Actions.Property(swapGroup, @cogA.Transform.WorldTranslation, forwardB, normalizedTime, Ease.Linear);
    Actions.Property(swapGroup, @cogB.Transform.WorldTranslation, forwardA, normalizedTime, Ease.Linear);
    
    // Move the tiles back into place
    var moveBackGroup = Actions.Group(seq);
    Actions.Property(moveBackGroup, @cogA.Transform.WorldTranslation, posB, forwardTime, Ease.Linear);
    Actions.Property(moveBackGroup, @cogB.Transform.WorldTranslation, posA, forwardTime, Ease.Linear);
    
    Actions.Call(seq, this.FinishedSwap);
  }
  
  function FinishedSwap()
  {
    this.Owner.DispatchEvent(Events.FinishedTileSwap, ZilchEvent());
  }
  
  function GetTilePosition(index : Integer) : Real3
  {
    var maxHandSize = PlayerHandManager.HandSize;
    var pos = this.Transform.WorldTranslation;
    var halfHandSize = maxHandSize / 2.0;
    var totalHandSize = maxHandSize * this.TileWidth + (maxHandSize - 1) * this.SpaceBetweenTiles;
    
    var planeBasisX = -this.Orientation.WorldRight;
    var planeBasisY = this.Orientation.WorldUp;
    
    // Compute the left edge of where tiles go
    var worldLeftEdge = pos - planeBasisX * totalHandSize / 2.0;
    // Compute the offset based upon the tile index
    var localXOffset = index * (this.TileWidth + this.SpaceBetweenTiles);
    var localYOffset = this.HeightOffset;
    var offset = Real3();
    offset += planeBasisX * localXOffset;
    offset += planeBasisY * localYOffset;
    return worldLeftEdge + offset;
  }
  
  function GetFaceDownWorldRotationForCog(cog : Cog) : Quaternion
  {
    var cogOrientation = cog.Orientation;
    return cogOrientation.GetLookAtDirectionWithUpRotation(-this.Orientation.WorldUp, this.Orientation.WorldForward);
  }
  
  function GetFaceUpWorldRotationForCog(cog : Cog) : Quaternion
  {
    var cogOrientation = cog.Orientation;
    return cogOrientation.GetLookAtDirectionWithUpRotation(this.Orientation.WorldForward, this.Orientation.WorldUp);
  }
}
